###
### ロゴ
###
### この章は、省略なしの英語で書かれています。
###

# BGMを停止する(先頭に戻った場合のため)
@bgm stop

# スキップを禁止して、ロゴ表示をスキップできなくする
@skip disable

# ステージを白い背景でクリアします
@bg file=white.png duration=0

# ロゴを表示します
@bg file=logo.png duration=0.5

# 2秒待ちます
@wait duration=2.0

# ロゴを消します
@bg file=white.png duration=0.5

# スキップを許可します
@skip enable

###
### メニュー
###
### この章は、省略なしの日本語で書かれています。
###

# BGMを再生する
@音量 トラック=音楽 音量=1.0
@音楽 ファイル=01.ogg

# メニューの背景を表示する
@背景 ファイル=title.png 1.0

# メニューを表示する
:MENU
@メニュー ファイル=title.txt

# メニューで"START"が選択されたとき、ここにジャンプしてくる
:START

# シーン1にジャンプする
@ジャンプ 行き先=シーン1

# メニューで"LOAD"が選択されたとき、ここにジャンプしてくる
:LOAD
@メニュー ファイル=load.txt cancel
@ジャンプ 行き先=MENU

# メニューで"CONFIG"が選択されたとき、ここにジャンプしてくる
:CONFIG
@メニュー ファイル=system.txt cancel
@ジャンプ 行き先=MENU

###
### シーン1 (セリフとモノローグの交錯)
###
### この章も、省略なしの日本語で書いています。
###

:シーン1

# 準備を行う
@章 タイトル=シーン1
@音量 トラック=音楽 音量=0.0 秒=1.0
@背景 ファイル=white.png 秒=1.0
@音楽 ファイル=03.ogg
@音量 トラック=音楽 音量=0.5 秒=1.0
@背景 ファイル=roof.png 秒=1.0 エフェクト=rule:suika.png

# 以降、キャラの表示、セリフ、音量の調節を行う

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*みどり*001.ogg*あー、気持ちいい風……。

@キャラ 位置=中央 消去 秒=1.0

*みどり*002.ogg*（私は今、夏休みの補習を抜け出して、学校の屋上に出ている。）

@キャラ 位置=中央 ファイル=001-happy.png 秒=1.0

*みどり*003.ogg*補習は出欠取らないから、別にいいんだ～。

@キャラ 位置=中央 消去 秒=1.0

*みどり*004.ogg*（辺りにビルは見あたらず、少し離れたところには海岸線が見える。ここは北海道の田舎町だ。）

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*みどり*005.ogg*どうして補習を抜け出したのかって？

@キャラ 位置=中央 消去 秒=1.0

*みどり*006.ogg*（あ、言い忘れてた。私の名前は\#{00ff00}みどり\#{DEF}。この田舎町で女子高生をやってる。家は農家だよ。）

@キャラ 位置=中央 ファイル=001-angry.png 秒=1.0

*みどり*007.ogg*それはね……

@キャラ 位置=中央 消去 秒=1.0

*みどり*008.ogg*（そうだった。とても大切なことがあったんだ。今日私がここにいる理由は……。）

@キャラ 位置=中央 ファイル=001-normal.png 秒=1.0

*みどり*009.ogg*君に……

@キャラ 位置=中央 ファイル=001-happy.png 秒=1.0

*みどり*010.ogg*君に……Suika2のことを知ってもらうためだよ！

###
### シーン2 (屋上であいさつする)
###
### この章は、省略ありの英語で記述されています。
### 慣れればこれくらい簡潔に書けますが、今は雰囲気だけ伝わればいいです。
###

@chapter シーン2

@bgm 02.ogg
@vol bgm 1.0 1.0

@ch c 001-happy.png 1.0

*みどり*011.ogg*こんにちは！Suika2を使ってくれてありがと！
*みどり*012.ogg*知ってると思うけど、Suika2は、アドベンチャーゲーム制作ツールだよ。
*みどり*013.ogg*自由に使えて、PCでもMacでも動くなんて、すごくない？

@ch c 001-fun.png 1.0

*みどり*014.ogg*作者が質問してほしいんだって。何か聞きたいことあるかな？

@ch c none 1.0

###
### シーン3 (屋上で質問を受け付ける)
###
### この章は、再び日本語で書かれています。英語とどちらがお好みですか？
###

@章 タイトル=シーン3

:LOOP1
@選択肢 行き先1=L1_ADVANTAGE 選択肢1=何が強みなの？ 行き先2=L1_ENV 選択肢2=動作環境は？ 行き先3=L1_NEXT 選択肢3=次へ

:L1_ADVANTAGE
x-engineの強みは、三つあります。
第一は、移植性。iPhone, iPad, Android, PC, Mac, ブラウザ, ゲーム機のアプリを作ることができます。
第二は、シンプルさ。小説に演出を付けることで、誰でも簡単にゲームを作成可能です。
第三は、自由さ。商用エンジンであるにもかかわらず、無償で自由に利用可能です。

# 1番のフラグに1をセットする(既読をマーク)
@フラグをセット $1 = 1

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*みどり*015.ogg*へー、そうだったんだ。私も知らなかったよ。
*みどり*016.ogg*ほかにも聞きたいことある？

@キャラ 位置=中央 消去 秒=1.0

@ジャンプ 行き先=LOOP1

:L1_ENV
iPhone, iPad, Android, PC, ゲーム機で快適に動作します。
x-engineで制作されたゲームが続々とストア公開されています。

# 2番のフラグに1をセットする(既読をマーク)
@フラグをセット $2 = 1

@キャラ 位置=中央 ファイル=001-fun.png 秒=1.0

*みどり*017.ogg*うんうん、そうなんだよね。
*みどり*018.ogg*まだまだ質問してほしいってさ。

@ジャンプ 行き先=LOOP1

:L1_NEXT

###
### シーン4 (浜辺で質問を受け付ける)
###
### この章は、省略ありの日本語で記述されています。
### 「ファイル=」などの記述がありません。
###

@章 シーン4

@キャラ 中央 001-fun.png 1.0

*みどり*019.ogg*学校出て、ちょっと歩こうか。どこに行く？浜の方に出てみようか。

@音量 音楽 0.0 1.0

@背景 coast.png 1.0 右カーテン

@音量 音楽 0.5 0.0

@音楽 03.ogg

@ch c 001-happy.png 1.0

*みどり*020.ogg*ほかにも知りたいこととかある？

@ch c none 1.0

:LOOP2
@選択肢 L2_OLD いつから作ってるの？ L2_CUSTOMIZE カスタマイズできるの？ L2_NEXT 次へ

:L2_OLD
23年くらい前からです。

# 3番のフラグに1をセットする(既読をマーク)
@フラグをセット $3 = 1

@キャラ 中央 001-fun.png 1.0

*みどり*015.ogg*へー、そうだったんだ。私も知らなかったよ。
*みどり*018.ogg*まだまだ質問してほしいってさ。

@キャラ 中央 消去 1.0

@ジャンプ LOOP2

:L2_CUSTOMIZE
フォント、メッセージボックス、セーブ画面などがカスタマイズ可能です。
もし、カスタマイズしたいけどできない、という項目があれば、ご連絡ください。

# 4番のフラグに1をセットする(既読をマーク)
@フラグをセット $4 = 1

@キャラ 中央 001-fun.png 1.0

*みどり*017.ogg*うんうん、そうなんだよね。
*みどり*016.ogg*ほかにも聞きたいことある？

@キャラ 中央 消去 1.0

@ジャンプ LOOP2

:L2_NEXT

###
### シーン5 (浜でラスト・セリフとモノローグの交錯)
###
### この章は、省略なしの英語で書かれています。
###

@chapter title=シーン5

@ch position=c file=001-fun.png duration=1.0

*みどり*021.ogg*今日はいろいろ聞いてくれてありがとうね。

@ch position=center file=none duration=1.0

*みどり*022.ogg*（君に、Suika2のことを知ってもらいたくて……）

@ch position=center file=001-fun.png duration=1.0

*みどり*023.ogg*何かわからないことがあったら、いつでも\#{ff0000}midori@luxion.jp\#{DEF} までメールしてよ。

@ch position=center file=none duration=1.0

*みどり*024.ogg*（……本当にメールしていいんだからね？　メールしてくれるかな？）

@ch position=center file=001-fun.png duration=1.0

*みどり*025.ogg*それじゃ、また会おうね！

# メッセージボックスを揺らす
@anime msgbox-shake.txt sync,showmsgbox

###
### シーン6 (おまけ)
###
### この章は、日本語を使っています。
###

# セットされていないフラグがあればENDにジャンプする
@フラグでジャンプ $1 != 1 END
@フラグでジャンプ $2 != 1 END
@フラグでジャンプ $3 != 1 END
@フラグでジャンプ $4 != 1 END

# 全部のフラグがセットされているときはここに来る
@章 タイトル=おまけ

全部読んでいただいてありがとうございました。
おまけとして雨エフェクトを披露します。

# 雨が降る
&start-rain

雨のエフェクトがあります。

# 雨がやむ
&stop-rain

# 雪が降る
&start-snow

雪のエフェクトもあります。

# 雪がやむ
&stop-snow

@goto END

###
### エフェクトの取り込み
###  - 終了のラベルより手前で、直接実行されない箇所に書く
###

using library/effect.txt

###
### 終了
###
### この章は、省略ありの英語を使っています。
###

:END

# init.txtを読み込み直して、先頭から実行する
@load init.txt

###
### いかがでしたか？いろいろな記法がありますが、お好みのものをお使いください！
###
